// Slurp demonstration.
// Demonstrates file 'slurping' - Loading an entire file into a string at once.
// Just to prove there is some slurping going on, we reverse the entire string
// in memory using substrings.

fun slurp(path){
	var file = read(path);
	
	if(file == nil){
		return "";
	}
	
	var code = get(file);
	var result = "";
	
	while(code != nil){
		result = result + chr(code);
		code = get(file);
	}
	
	close(file);
	return result;
}

fun reverse(message){
	var result = "";
	
	for(var i = length(message) - 1; i >= 0; i = i - 1){
		result = result + substring(message, i, 1);
	}
	
	return result;
}

fun main(){
	if(args() == 2){
		var text = slurp(arg(1));
		text = reverse(text);
		print text;
	} else {
		print "Usage: slurp.krox <path>";
	}
}

main();
