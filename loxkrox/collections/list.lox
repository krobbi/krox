// A list of elements.
class List {
	// Initialize the list's list nodes.
	init(){
		// The list's front node.
		this.head = nil;
		
		// The list's back node.
		this.tail = nil;
	}
	
	// Clear the list.
	clear(){
		this.head = nil;
		this.tail = nil;
	}
	
	// Return whether the list has no elements.
	is_empty(){
		return this.head == nil;
	}
	
	// Append an element to the back of the list.
	push_back(element){
		var node = ListNode(element, this.tail);
		
		if(node.previous){
			node.previous.next = node;
		} else {
			this.head = node;
		}
		
		this.tail = node;
	}
	
	// Remove and return an element from the back of the list.
	pop_back(){
		var node = this.tail;
		
		if(!node){
			return nil;
		}
		
		if(node.previous){
			node.previous.next = nil;
			this.tail = node.previous;
		} else {
			this.clear();
		}
		
		return node.element;
	}
	
	// Return the list joined as a string with a separator.
	join(separator){
		var node = this.head;
		var string = "";
		
		while(node){
			string = string + node.element;
			node = node.next;
			
			if(node){
				string = string + separator;
			}
		}
		
		return string;
	}
	
	// Return a new list iterator for the list.
	iter(){
		return ListIterator(this.head);
	}
}
