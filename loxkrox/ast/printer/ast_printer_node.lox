// A generic tree node for printing an AST.
class ASTPrinterNode {
	// Initialize the AST printer node's message and child list.
	init(message){
		// The AST printer node's message.
		this.message = message;
		
		// The AST printer node's child list.
		this.children = List();
	}
	
	// Append an AST printer node to the AST printer node's children.
	append(node){
		this.children.push_back(node);
	}
	
	// Recursively print the AST printer node and its children.
	print_node(flags){
		var iter = flags.iter();
		var prefix = "";
		
		while(iter.has_next()){
			var flag = iter.get_next();
			
			if(iter.has_next()){
				if(flag){
					prefix = prefix + "|  ";
				} else {
					prefix = prefix + "   ";
				}
			} else {
				if(flag){
					prefix = prefix + "|--";
				} else {
					prefix = prefix + "|__";
				}
			}
		}
		
		print prefix + this.message;
		iter = this.children.iter();
		
		while(iter.has_next()){
			var child = iter.get_next();
			flags.push_back(iter.has_next());
			child.print_node(flags);
			flags.pop_back();
		}
	}
}
