// Slurp demonstration.
// Demonstrates file 'slurping' - Loading an entire file into a string at once.
// Just to prove there is some slurping going on, we reverse the entire string
// in memory using substrings.

fun slurp(path){
	var file = _read(path);
	
	if(file == nil){
		return "";
	}
	
	var code = _get(file);
	var result = "";
	
	while(code != nil){
		result = result + _chr(code);
		code = _get(file);
	}
	
	_close(file);
	return result;
}

fun reverse(message){
	var result = "";
	
	for(var i = _length(message) - 1; i >= 0; i = i - 1){
		result = result + _substring(message, i, 1);
	}
	
	return result;
}

fun main(){
	if(_args() == 2){
		var text = slurp(_arg(1));
		text = reverse(text);
		print text;
	} else {
		print "Usage: slurp.krox <path>";
	}
}

main();
