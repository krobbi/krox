// Cat demonstration.
// Demonstrates a program that echoes the contents of a file, or copies a source
// file to a target file.

fun cat(path, target){
	var source = _read(path);
	
	if(source == nil){
		print "Failed to open '" + path + "' for reading.";
		return;
	}
	
	var is_writing = true;
	var character = _get(source);
	
	while(is_writing and character != nil){
		if(_put(character, target)){
			character = _get(source);
		} else {
			print "Failed to write output.";
			is_writing = false;
		}
	}
	
	if(!_close(source)){
		print "Failed to close source file '" + path + "'.";
	}
}

fun main(){
	if(_args() == 2){
		cat(_arg(1), _stdout());
	} else if (_args() == 3){
		var source_path = _arg(1);
		var target_path = _arg(2);
		
		if(source_path == target_path){
			print "Cannot copy file to itself.";
			return;
		}
		
		var target = _write(target_path);
		
		if(target == nil){
			print "Failed to open '" + target_path + "' for writing.";
			return;
		}
		
		cat(source_path, target);
		
		if(!_close(target)){
			print "Failed to close target file '" + target_path + "'.";
		}
	} else {
		print "Usage:";
		print " * cat.lox <path>";
		print " * cat.lox <source> <target>";
	}
}

main();
